// Top-level build file where you can add configuration options common to all sub-projects/modules.
 

buildscript {

    repositories {
        google()
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'
        classpath 'com.google.gms:google-services:4.3.3'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.1'  
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

subprojects {
    buildscript {
        repositories {
        google()
        jcenter()
        mavenCentral()
        
            
        }
    }

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://jitpack.io" }
    
    }

    afterEvaluate {
        android {
            compileSdkVersion 29
            buildToolsVersion "29.0.3"

            defaultConfig {
                if (minSdkVersion == null)
                    minSdkVersion 14
                targetSdkVersion 28
            }

            compileOptions {
                sourceCompatibility JavaVersion.VERSION_1_8
                targetCompatibility JavaVersion.VERSION_1_8
            }
        }

        if (getPlugins().hasPlugin('com.android.library')) {
            task javadoc(type: Javadoc) {
                failOnError false
                title = 'butterknife API'
                exclude '**/internal/**'
                source = android.sourceSets.main.java.sourceFiles
                classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
                classpath += configurations.compile
                options.links('https://docs.oracle.com/javase/8/docs/api/')
                options.links('https://developer.android.com/reference/')
                options.noDeprecated = true
            }

            task sourcesJar(type: Jar) {
                classifier = 'sources'
                from android.sourceSets.main.java.sourceFiles
            }

            task javadocJar(type: Jar, dependsOn: javadoc) {
                classifier = 'javadoc'
                from javadoc.destinationDir
            }

            artifacts {
                archives javadocJar
                archives sourcesJar
            }
        }
    }
}



